!to "engine.prg", cbm   ; output program
!sl "labels.l"          ; create label file for vice
!cpu 6510               ; for illegal opcodes
!convtab scr            ; for conversion to c64 screen codes

!source "lib/mem.acme"
!source "lib/vic.acme"
!source "lib/cia.acme"
!source "lib/std.acme"

CONTROL_X           = %00010000
CONTROL_Y           = %00110000
CONTROL_Y_INVALID   = %01110000
CONTROL_Y_MASK      = %10111111
LINE_0              = FIRST_BADLINE-3
SCROLL_SPEED        = 2
SLOWDOWN            = 2

*=$0700
; init multiplexer data
!by $00, $01, $02, $03, $04, $05, $06, $07, $08, $09, $0a, $0b, $0c, $0d, $0e, $0f; SPR_INDEX
!by $10, $20, $30, $40, $50, $60, $70, $80, $10, $20, $30, $40, $50, $60, $70, $80; SPR_0_X
!by $30, $30, $30, $30, $40, $40, $40, $40, $50, $50, $50, $50, $60, $60, $60, $60; SPR_0_Y
!by $f0, $e1, $d2, $c3, $b4, $a5, $96, $87, $78, $69, $5a, $4b, $3c, $2d, $1e, $0f; SPR_0_COLOR
!by $80, $82, $84, $86, $88, $8a, $8c, $8e, $90, $92, $94, $96, $98, $9a, $9c, $9e; SPR_0_PTR
!by $00, $00, $00, $00, $00, $00, $00, $00, $00, $00, $00, $00, $00, $00, $00, $00; SPR_0_FLAGS

!if * != STRIP_0_X {
    !error "incorrect table size"
}

START               = $080b
+create_basic_starter START
*=START

!zone {
    ; select VIC area: bank 1
    lda CIA2_DATA_PORT_A
    and #%11111100
    ora #%00000010
    sta CIA2_DATA_PORT_A

    ; select screen bank 15, hires-bank 0
    lda #%11110000
    sta VIC_ADDR_SELECT

;-------------------------------------------------------------------------------
;   disable all basic, kernal and irq crap
;-------------------------------------------------------------------------------
    ; disable IRQs
    sei

    ; disable ROMs
    +set RAM_ROM_ALL_RAM_WITH_IO, RAM_ROM_SELECTION

    ; ack all interrupts which might have happend
    +set $ff, VIC_IRQ_STATUS
    lda CIA1_INTERRUPT_CONTROL
    lda CIA2_INTERRUPT_CONTROL

    ; set empty interrupt routine
    +set16 EMPTY_INTERRUPT, VECTOR_NMI
    +disable_timer_interrupts

    ; set timer A to 0
    lda #0
    sta CIA2_TIMER_A_LO
    sta CIA2_TIMER_A_HI

    ; trigger timer A interrupt
    +set %10000001, CIA2_INTERRUPT_CONTROL
    +set %00000001, CIA2_CONTROL_TIMER_A

    ; reset stack
    ldx #$ff
    txs

    ; just clear this flag and never ever touch this again
    cld

    ; enable IRQs
    cli

    ; -> NMI is disabled as soon as EMPTY_INTERRUPT has been triggered since we will never ever ack NMI again
    ; -> zero page completely free on this spot except for the special regs $00 and $01

;-------------------------------------------------------------------------------
;   setup sprites
;-------------------------------------------------------------------------------

    +set %10101010, VIC_SPR_MULTI
    +set RED, VIC_SPR_COLOR_01
    +set WHITE, VIC_SPR_COLOR_11

    ; zero-fill sprite frame
    lda #0
    ldx #63
-
    sta VIC_BANK - 128, x
    dex
    bpl -

    ; set sprite pointers in that bank
    lda #$fe
    ldx # (SPR_FR % $4000) / 64
    !for .i, 0, 7 {
        sta SPR_PTR - $4000 + .i
    }

    +set $ff, VIC_SPR_ENABLE

;-------------------------------------------------------------------------------
;   setup raster irq
;-------------------------------------------------------------------------------

    sei
        +set16 IRQ, VECTOR_IRQ
        +set_raster_line_9 LINE_0
        +set 1, VIC_IRQ_CONTROL
    cli

-
    ; some 7 cycle garbage instructions
    lda ($ff), y
    lda ($ff, x)
    lda ($ff), y
    lda ($ff, x)
    lda ($ff), y
    sec     ; 2
    +bcs    ; 3
    jmp -
}

!source "scroll.acme"
!source "joystick.acme"
!source "raster.acme"

*=HIRES

!for .i, $0000, $1fff {
    !by <.i
}

!for .i, 0, 15 {
    +sprite_line %.............#.#.#.#.#.#
    +sprite_line %.............#.#.#.#.#.#
    +sprite_line %.............#.#.#.#.#.#
    +sprite_line %.............#.#.#.#.#.#
    +sprite_line %.............#.#.#.#.#.#
    +sprite_line %.............#.#.#.#.#.#
    +sprite_line %.............#.#.#.#.#.#
    +sprite_line %.............#.#.#.#.#.#
    +sprite_line %.............#.#.#.#.#.#
    +sprite_line %.............#.#.#.#.#.#
    +sprite_line %........................
    +sprite_line %#.#.#.#.#.#.############
    +sprite_line %#.#.#.#.#.#.############
    +sprite_line %#.#.#.#.#.#.############
    +sprite_line %#.#.#.#.#.#.############
    +sprite_line %#.#.#.#.#.#.############
    +sprite_line %#.#.#.#.#.#.############
    +sprite_line %#.#.#.#.#.#.############
    +sprite_line %#.#.#.#.#.#.############
    +sprite_line %#.#.#.#.#.#.############
    +sprite_line %#.#.#.#.#.#.############
    !by $00; padding
    +sprite_line %########################
    +sprite_line %#.#..................#.#
    +sprite_line %#..#................#..#
    +sprite_line %#...#..............#...#
    +sprite_line %#....#............#....#
    +sprite_line %#.....#..........#.....#
    +sprite_line %#......#........#......#
    +sprite_line %#.......#......#.......#
    +sprite_line %#........#....#........#
    +sprite_line %#.........#..#.........#
    +sprite_line %#..........##..........#
    +sprite_line %#.........#..#.........#
    +sprite_line %#........#....#........#
    +sprite_line %#.......#......#.......#
    +sprite_line %#......#........#......#
    +sprite_line %#.....#..........#.....#
    +sprite_line %#....#............#....#
    +sprite_line %#...#..............#...#
    +sprite_line %#..#................#..#
    +sprite_line %#.#..................#.#
    +sprite_line %########################
    !by $00; padding
}

*=SCREEN

!for .i, $0000, $03ff {
    !by <.i
}
