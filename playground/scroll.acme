INC_HARD_Y !zone {
    inc HARD_Y
    lda #25
    cmp HARD_Y
    bne .return

    lda #0
    sta HARD_Y

    lda HARD_X
    sec
    sbc #16
    sta HARD_X
    clc
    adc #40
    cmp #39
    +bae +
    sta HARD_X
    rts
+
    dec SCREEN_Y

.return
    rts
}

DEC_HARD_Y !zone {
    dec HARD_Y
    bpl .return

    lda #24
    sta HARD_Y

    lda HARD_X
    clc
    adc #16
    sta HARD_X
    sec
    sbc #40
    bmi +
    sta HARD_X
    rts
+
    inc SCREEN_Y

.return
    rts
}

!macro wrap_inc_2 .mem, .l {
    lda .mem
    clc
    adc #2
    and #%00000111
    sta .mem
    bne .l
}

!macro wrap_dec_2 .mem, .l {
    lda .mem
    sec
    sbc #2
    and #%00000111
    sta .mem
    +bbc .l
}

SCROLL_U !zone {
    +wrap_dec_2 SOFT_Y, +
    jsr DEC_HARD_Y
+
    rts
}

SCROLL_D !zone {
    +wrap_inc_2 SOFT_Y, +
    jsr INC_HARD_Y
+
    rts
}

SCROLL_L !zone {
    +wrap_inc_2 SOFT_X, +

    inc HARD_X
    lda #40
    cmp HARD_X
    bne +

    lda #0
    sta HARD_X
    jsr DEC_HARD_Y
+
    rts
}

SCROLL_R !zone {
    +wrap_dec_2 SOFT_X, +

    dec HARD_X
    bpl +

    lda #39
    sta HARD_X
    jsr INC_HARD_Y
+
    rts
}
