!macro wrap_inc_2 .mem, .l {
    lda .mem
    clc
    adc #2
    and #%00000111
    sta .mem
    bne .l
}

INC_HARD_Y !zone {
    inc HARD_Y
    lda #25
    cmp HARD_Y
    bne .return

    lda #0
    sta HARD_Y

    lda HARD_X
    sec
    sbc #16
    sta HARD_X
    clc
    adc #40
    bpl +
    sta HARD_X
    jmp ++
+
    dec SCREEN_Y
++
.return
    rts
}

DEC_HARD_Y !zone {
    dec HARD_Y
    bpl .return

    lda #24
    sta HARD_Y

    lda HARD_X
    clc
    adc #16
    sta HARD_X
    sec
    sbc #40
    bmi +
    sta HARD_X
    jmp ++
+
    inc SCREEN_Y
++
.return
    rts
}

SCROLL_U !zone {
    dec SOFT_Y
    dec SOFT_Y
    bpl +
    lda #6
    sta SOFT_Y
    jsr DEC_HARD_Y
+
    rts
}

SCROLL_D !zone {
    +wrap_inc_2 SOFT_Y, +
    jsr INC_HARD_Y
+
    rts
}

SCROLL_L !zone {
    +wrap_inc_2 SOFT_X, +

    inc HARD_X
    lda #40
    cmp HARD_X
    bne +

    lda #0
    sta HARD_X
    jsr DEC_HARD_Y
+
    rts
}

SCROLL_R !zone {
    dec SOFT_X
    dec SOFT_X
    bpl +
    lda #6
    sta SOFT_X
    dec HARD_X
    bpl +

    lda #39
    sta HARD_X
    jsr INC_HARD_Y
+
    rts
}
